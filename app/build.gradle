buildscript {
    repositories {
        maven { url 'https://maven.google.com' }
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

def versionMajor = 6
def versionMinor = 5
def versionPatch = 0
def versionBuild = 5

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
apply plugin: 'hugo'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
}

def gitSha = { ->
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitBranchName = { ->
    def p = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    lintOptions {
        baseline file("lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
    }

    dexOptions {
        jumboMode true
    }

    defaultConfig {
        applicationId "com.boardgamegeek"

        minSdkVersion 15
        targetSdkVersion 25

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        def buildTime = new GregorianCalendar().format("yyyy-MM-dd h:mm:ss a z")
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'String', 'GIT_BRANCH', "\"${gitBranchName()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (gitBranchName() != "master") {
                versionNameSuffix " RC${versionBuild}"
            }
        }
        debug {
            if (versionBuild > 0) {
                versionNameSuffix " build ${versionBuild} (${gitBranchName()})"
            } else {
                versionNameSuffix " (${gitBranchName()})"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:palette-v7:25.3.1'
    compile 'com.android.support:percent:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile('com.squareup.retrofit2:converter-simplexml:2.2.0') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    compile('com.mikepenz:aboutlibraries:5.9.5@aar') {
        transitive = true
    }
    compile 'org.greenrobot:eventbus:3.0.0'
    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    compile 'com.github.lzyzsd.randomcolor:library:1.0.0'
    compile 'com.appyvet:materialrangebar:1.3'
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    compile 'org.jsoup:jsoup:1.10.2'

    provided "com.google.auto.value:auto-value:1.4.1"
    apt "com.google.auto.value:auto-value:1.4.1"

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.10'

    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}

apt {
    arguments {
        eventBusIndex "com.boardgamegeek.events.BggEventBusIndex"
    }
}
